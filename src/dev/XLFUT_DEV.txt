/**
Convert tests to export format.
*/
EXPORTTESTS = LAMBDA(testsRange,
    LET(
        header, HSTACK("description", "cell_for_ref", "formula", "message", "assert"),
        headerWithSequence, VSTACK(header, SEQUENCE(1, COLUMNS(header), 1, 1)),
        indexOfDescriptionColumn, INDEX(HLOOKUP("description", headerWithSequence, 2, FALSE), 1, 1),
        indexOfCellForRefColumn, INDEX(HLOOKUP("cell_for_ref", headerWithSequence, 2, FALSE), 1, 1),
        indexOfFormulaColumn, INDEX(HLOOKUP("formula", headerWithSequence, 2, FALSE), 1, 1),
        indexOfMessageColumn, INDEX(HLOOKUP("message", headerWithSequence, 2, FALSE), 1, 1),
        indexOfAssertColumn, INDEX(HLOOKUP("assert", headerWithSequence, 2, FALSE), 1, 1),
        currentHeader, INDEX(testsRange, 1,),
        currentHeaderWithSequence, VSTACK(currentHeader, SEQUENCE(1, COLUMNS(currentHeader), 1, 1)),
        currentIndexOfDescriptionColumn, INDEX(HLOOKUP(Index(header,, indexOfDescriptionColumn), currentHeaderWithSequence, 2, FALSE), 1, 1),
        currentIndexOfCellForRefColumn, INDEX(HLOOKUP(Index(header,, indexOfCellForRefColumn), currentHeaderWithSequence, 2, FALSE), 1, 1),
        currentindexOfFormulaColumn, INDEX(HLOOKUP(Index(header,, indexOfFormulaColumn), currentHeaderWithSequence, 2, FALSE), 1, 1),
        currentindexOfMessageColumn, INDEX(HLOOKUP(Index(header,, indexOfMessageColumn), currentHeaderWithSequence, 2, FALSE), 1, 1),
        currentindexOfAssertColumn, INDEX(HLOOKUP(Index(header,, indexOfAssertColumn), currentHeaderWithSequence, 2, FALSE), 1, 1),
        descriptionColumn, INDEX(testsRange,, currentIndexOfDescriptionColumn),
        cellForRefColumn, INDEX(testsRange,, currentIndexOfCellForRefColumn),
        formulaColumn, INDEX(testsRange,, currentindexOfFormulaColumn),
        messageColumn, INDEX(testsRange,, currentindexOfMessageColumn),
        assertColumn, INDEX(testsRange,, currentindexOfAssertColumn),
        convertedRange, HSTACK(
            VSTACK(indexOfDescriptionColumn, IFS(ISBLANK(descriptionColumn), "", TRUE, descriptionColumn)),
            VSTACK(indexOfCellForRefColumn, IFS(ISBLANK(cellForRefColumn), "", TRUE, cellForRefColumn)),
            VSTACK(indexOfFormulaColumn, IFS(ISBLANK(formulaColumn), "", ISFORMULA(formulaColumn), FORMULATEXT(formulaColumn), TRUE, formulaColumn)),
            VSTACK(indexOfMessageColumn, INDEX(header, 1, indexOfMessageColumn), MAKEARRAY(ROWS(testsRange) - 1, 1, LAMBDA(rowIndex, columnIndex, ""))),
            VSTACK(indexOfAssertColumn, IFS(ISBLANK(assertColumn), "", ISFORMULA(assertColumn), FORMULATEXT(assertColumn), TRUE, assertColumn))
        ),
        convertedTests, DROP(TRANSPOSE(SORT(TRANSPOSE(FILTER(convertedRange, INDEX(convertedRange,, 1) <> "")), 1)), 2),
        VSTACK(
            header,
            convertedTests
        )
    )
);
